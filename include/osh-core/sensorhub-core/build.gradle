plugins {
  id 'java-test-fixtures'
}
description = 'OSH Core APIs & Components'
ext.details = 'Core components of OpenSensorHub framework'

rootProject.allprojects {
    repositories {
        maven {
            name 'GeoTools'
            url "https://maven.geo-solutions.it"
        }
    }
}

dependencies {
  embeddedApi project(':swe-common-core')
  embeddedApi project(':swe-common-om')
  embeddedApi project(':sensorml-core')
  api 'org.osgi:org.osgi.framework:1.10.0'
  embeddedApi 'org.eclipse.jetty:jetty-servlet:9.4.57.v20241219'
  embeddedApi 'org.eclipse.jetty:jetty-servlets:9.4.57.v20241219'
  embeddedApi 'org.eclipse.jetty.websocket:websocket-server:9.4.57.v20241219'
  embeddedApi 'org.eclipse.jetty:jetty-xml:9.4.57.v20241219'
  embeddedApi 'javax.validation:validation-api:2.0.1.Final'
  embeddedApi 'dev.failsafe:failsafe:3.3.2'
  embeddedApi('org.geotools:gt-cql2-text:C105.33.2.00') {
      exclude group: "javax.media", module: "jai_core"
      exclude group: "it.geosolutions.jgridshift", module: "jgridshift-core"
  }
  
  testImplementation project(path: ':sensorml-core', configuration: 'testArtifacts')
  testImplementation 'commons-io:commons-io:1.3.2'

  testFixturesImplementation 'junit:junit:4.13.1'
}

test {
	environment 'SimpleEnvironment', 'value2'
	environment 'DuplicateKey', 'value4'
}

// add info to OSGI manifest
osgi {
  manifest {
    attributes 'Bundle-Vendor': 'Sensia Software LLC'
    attributes 'Bundle-Activator': 'org.sensorhub.impl.Activator'
    
    // allow binding to OWS reader/writer dynamically (needed by OGCRegistry)
    attributes('DynamicImport-Package': 'org.vast.ows,org.vast.ows.*')
  }
}

// add info to maven pom
ext.pom >>= {
  developers {
    developer {
      id 'alexrobin'
      name 'Alex Robin'
      organization 'Sensia Software LLC'
      organizationUrl 'http://www.sensiasoftware.com' 
    }
  } 
}

task packageTests(type: Jar) {
  from sourceSets.test.output
  archiveClassifier = 'tests'
}

task sourcesJar(type: Jar, dependsOn: classes) {
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  archiveClassifier = 'javadoc'
  from javadoc.destinationDir
}

configurations {
  testArtifacts
}

artifacts {
  testArtifacts packageTests
  archives sourcesJar
  archives javadocJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifact sourcesJar 
      artifact javadocJar
    }
  }
}
